pkgname=dwm-git
_pkgname=dwm
pkgver=6.1.2.g3465bed
pkgrel=4
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft')
makedepends=('git')
install=dwm.install
provides=('dwm')
conflicts=('dwm')
epoch=1
source=(dwm.desktop
        "$_pkgname::git+http://git.suckless.org/dwm#tag=6.1.2-g3465bed"
        dwm-6.1-systray.diff 
        dwm-6.1-pertag.diff
        dwm-6.1-better-borders.diff
        config.h)
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

pkgver(){
  cd $_pkgname
  git describe --tags |sed 's/-/./g'
}

prepare() {
  cd $_pkgname
  cp -f ../config.h config.h
  patch -p1 < ../dwm-6.1-systray.diff
  patch -p1 < ../dwm-6.1-pertag.diff
  patch -p1 < ../dwm-6.1-better-borders.diff
}

build() {
  cd $_pkgname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D ../dwm.desktop "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
